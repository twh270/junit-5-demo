plugins {
	id 'java'
	id 'eclipse' // optional (to generate Eclipse project files)
	id 'idea' // optional (to generate IntelliJ IDEA project files)
}

repositories {
	mavenCentral()
}

dependencies {
	testImplementation('org.junit.jupiter:junit-jupiter:5.6.2')
}

test {
    // To enable a system property use e.g. gradlew test -Denv=ci
    systemProperties = System.properties
    
	useJUnitPlatform {
		excludeTags 'experimental'
	}
	testLogging {
		events "passed", "skipped", "failed"
	}
}

/*
  Define a task to run experimental tests
  gradlew test             --> runs tests that are not tagged 'experimental'
  gradlew experimentalTest --> runs only tests that are tagged 'experimental'
  gradlew check            --> runs 'test' followed by 'experimentalTest'
*/

tasks.create('experimentalTest', Test) {
    systemProperties System.properties
    useJUnitPlatform {
        includeTags 'experimental'
    }
	testLogging {
		events "passed", "skipped", "failed"
	}
    shouldRunAfter test
}

tasks.named("check") {
    dependsOn experimentalTest
}
